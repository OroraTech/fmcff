# © Copyright 2023 OroraTech GmbH
# Licensed under the terms and conditions of the Apache 2.0 license.
#

#
# Include and create micropython library
#
set(MICROPY_DIR ${CMAKE_CURRENT_LIST_DIR}/micropython)
include(${MICROPY_DIR}/py/py.cmake)
include(${MICROPY_DIR}/py/usermod.cmake)
include(${CMAKE_CURRENT_LIST_DIR}/modules/micropython.cmake)

add_library(micropython STATIC ${MICROPY_SOURCE_PY}
    mpy_helpers.cpp
    mphalport.cpp
    micropython/shared/runtime/gchelper_generic.c)

target_compile_definitions(micropython PUBLIC -DNDEBUG)

# for micropython headers:
target_include_directories(micropython PUBLIC ${MICROPY_DIR})

# for port configuration
target_include_directories(micropython PUBLIC ${CMAKE_CURRENT_LIST_DIR})

# for headers generated by micropython (version, QStr)
target_include_directories(micropython PUBLIC ${CMAKE_BINARY_DIR})

#
# Generate QStrings using mkrules.cmake
#
set(MICROPY_TARGET micropython)
set(MICROPY_SOURCE_QSTR
    ${MICROPY_SOURCE_PY}
    ${MICROPY_SOURCE_USERMOD}
    ${MICROPY_DIR}/shared/runtime/gchelper.h
    ${MICROPY_DIR}/shared/runtime/pyexec.c
)
include(${MICROPY_DIR}/py/mkrules.cmake)

target_link_libraries(micropython PUBLIC usermod)
target_link_libraries(micropython PRIVATE stm_hal)
